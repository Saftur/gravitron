class Guard : ZilchComponent
{
    [Dependency]
    var Socks : Physocks = null;
    
    [Property]
    var Active : Boolean = true;
    
    [Property]
    var LeftPoint : Real3 = Real3(0,0,0);
    [Property]
    var RightPoint : Real3 = Real3(1,0,0);
    
    [Property]
    var Type : Integer = 1; //Differentiate types of guards here
    
    //Stats
    [Property]
    var VisionRange : Real = 5;
    [Property]
    var ChaseSpeed : Real = 6;
    [Property]
    var JumpHeight : Real = 1.5;
    
    var Dir : Real3 = Real3(1,0,0);
    
    var OldPos : Real3 = Real3(0,0,0);
    var Stuck : Real = 0;
    var Time : Real = 0;
    
    var Player : Cog = null;
    var VisionIndicator : Cog = null;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        this.Player = this.Space.FindObjectByName("Player");
        
        var visions = this.Space.FindAllObjectsByName("GuardV");
        foreach (var v in visions)
        {
            if(v.Tag.Number == this.Owner.Tag.Number)  
            {
                this.VisionIndicator = v;
                break;
            }
        }
        
        this.VisionIndicator.Transform.Scale = Real3(this.VisionRange, this.Owner.Transform.Scale.Y /*height*/, 1);
        this.JumpHeight = this.Owner.Transform.Scale.Y/2 + 0.5;
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        this.Time += event.Dt;
        
        if(!this.Active) return;
        //Console.WriteLine("`this.RightPoint` `this.LeftPoint` `this.Dir`");
        
        var ray = Ray();
        var pos = this.Owner.Transform.Translation;
        var h = this.Owner.Transform.Scale.Y;
        
        Console.WriteLine("`this.Owner.RigidBody.Velocity`");
        ray.Direction = this.Dir;
        
        if(this.Look())
        {
            this.Chase();
        }
        else
        {
            this.Patrol();
        }
        Console.WriteLine("`this.Owner.RigidBody.Velocity`");
        //Console.WriteLine("`this.OldPos` `this.Owner.Transform.Translation`");
        
        //Getting Stuck
        if(Math.Abs(this.OldPos.X - this.Owner.Transform.Translation.X) <= 1)
        {
            this.Stuck += event.Dt;
        }
        if(this.Stuck > 2)      //If going one direction gets you nowhere turn around
        {
            this.Dir *= Real3(-1,1,1);
            this.Turn();
        }
        if(this.Time > 3) //Not stuck
        {
            this.OldPos = this.Owner.Transform.Translation;
            this.Time = 0;
            this.Stuck = 0;
        }
        
        //Jumping
        if(this.Socks.Grounded())
        {
            ray.Start = pos + Real3(0, this.JumpHeight, 0); //Check if object is jumpable
            var dist = this.Owner.Transform.Scale.X/2 + 0.75;
            if(this.CastRay(ray, dist, null) && !this.CastRay(ray,dist,this.Player))
            {
                this.Dir *= Real3(-1,1,1);
                this.Turn();
            }
            else
            {
                ray.Start = pos;
                if(this.CastRay(ray, dist, null) && !this.CastRay(ray, dist, this.Player))  //middle
                {
                    this.Socks.Jump();
                }
                
                ray.Start = pos + Real3(0, h/2, 0);
                if(this.CastRay(ray, dist, null) && !this.CastRay(ray, dist, this.Player)) //top
                {
                    this.Socks.Jump();
                }
                
                ray.Start = pos + Real3(0, -h/3, 0);
                if(this.CastRay(ray, dist, null) && !this.CastRay(ray, dist, this.Player)) //bottom
                {
                    this.Socks.Jump();
                }
            }
        }
    }
    function Chase()
    {
        var d = this.Player.Transform.Translation.X - this.Owner.Transform.Translation.X;
        d = this.Normalize(d);
        this.Dir = Real3(d,0,0);
        this.Turn();
        var speed = this.Socks.Speed;
        this.Socks.Speed = this.ChaseSpeed;
        this.Socks.Update(this.Dir);
        this.Socks.Speed = speed;
    }
    function Patrol()
    {
        //Patrolling
        var pos = this.Owner.Transform.Translation;
        if(pos.X > this.RightPoint.X)
        {
            this.Dir = Real3(-1,0,0);
            this.Turn();
        }
        if(pos.X < this.LeftPoint.X)
        {
            this.Dir = Real3(1,0,0);
            this.Turn();
        }
        this.Socks.Update(this.Dir);
    }
    function Turn()
    {
        this.Owner.RigidBody.Velocity = Real3(this.Owner.RigidBody.Velocity.X, 0, 0);
        if(this.Dir.X > 0)
        {
            this.Owner.Sprite.FlipX = false;
            this.VisionIndicator.FollowObject.Displacement = Real3(3,0,0);
        }
        else
        {
            this.Owner.Sprite.FlipX = true;
            this.VisionIndicator.FollowObject.Displacement = Real3(-3,0,0);
        }
    }
    function Look() : Boolean
    {
        //Console.WriteLine("`this.Player.PlayerStatus.Spotted`");
        var disFromPlayer = this.Player.Transform.Translation.X - this.Owner.Transform.Translation.X;
        if(this.Player.PlayerStatus.Spotted && Math.Abs(disFromPlayer) <= 10 && this.Dir.X == this.Normalize(disFromPlayer)) return true;
        
        var ray = Ray();
        var pos = this.Owner.Transform.Translation;
        var h = this.Owner.Transform.Scale.Y;
        
        ray.Direction = this.Dir;
        
        ray.Start = pos;
        if(this.CastRay(ray, this.Owner.Transform.Scale.X/2 + this.VisionRange, this.Player)) return true; //middle
            
        ray.Start = pos + Real3(0, h/2, 0);
        if(this.CastRay(ray, this.Owner.Transform.Scale.X/2 + this.VisionRange, this.Player)) return true; //top
        
        ray.Start = pos + Real3(0, -h/2 +0.1, 0);
        if(this.CastRay(ray, this.Owner.Transform.Scale.X/2 + this.VisionRange, this.Player)) return true; //bottom
        
        return false;
    }
    function CastRay(ray : Ray, dis : Real, cog : Cog) : Boolean
    {
        var objHit = this.Space.PhysicsSpace.CastRayFirst(ray);
        if(objHit != null && objHit.Distance <= dis)
        {
            if(cog != null && objHit.ObjectHit == cog)
            {
                return true;
            }
            else if(cog == null)
            {
                //Console.WriteLine("`objHit.ObjectHit` `objHit.Distance`");
                return true;
            }
        }
        return false;
    }
    function Normalize(r : Real) : Real
    {
        if(r > 0) r = 1;
        else r = -1;
        return r;
    }
}