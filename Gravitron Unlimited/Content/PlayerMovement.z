// Name: Charles
// Date: 2/2/17
// All content Â© 2017 DigiPen (USA) Corporation, all rights reserved.

class PlayerMovement : ZilchComponent
{
    [Dependency]
    var Socks : Physocks = null;
    [Dependency]
    var Collider : BoxCollider = null;
    var CeilingDistance : Real = 0;
    var ColliderSize : Real3 = Real3(1, 1.5, 1);
    var ColliderCrawlSize : Real3 = Real3(2, 1, 1);
    var CrawlOffset : Real3 = Real3(0, -0.25, 0);
    
    var Grabbox : Cog = null;    //For holding items
    var Displacement : Real3 = Real3(1.5,0,0);
    
    var Controls : Input;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        this.Grabbox = this.Space.FindObjectByName("Grabbox");
        this.Controls = new Input();
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        if(this.Owner.PlayerStatus.Caught)
        {
            return;
        }
        var movement = Real3(0,0,0);
        
        //Jumping
        if(this.Controls.KeyDown(2) && this.Socks.Grounded() && !this.Owner.PlayerStatus.Pulling && !this.Socks.UnderBlock())
        {
            this.Owner.Physocks.Jump();
        }
        else if(this.Controls.KeyReleased(2))
        {
            //this.Owner.SweptController.JumpCancel();
        }
        
        if (Zero.Keyboard.KeyIsDown(Keys.Enter)) {
            Console.WriteLine("`this.Socks.Grounded()` `!this.Socks.InPit()` `!this.Owner.PlayerStatus.Pulling`");
            Console.WriteLine("`this.Socks.UnderBlock()`");
        }
        
        //Crawling
        if(this.Controls.KeyDown(3) && this.Socks.Grounded() && !this.Socks.InPit() && !this.Owner.PlayerStatus.Pulling)
        {
            //this.Speed = 5;        //Crawling Speed
            //Crawling Sprite
            this.Owner.Sprite.Color = Real4(1,0,0,1);
            this.Collider.Offset = this.CrawlOffset;
            this.Collider.Size = this.ColliderCrawlSize;
            
        }
        else if (!this.Socks.UnderBlock())
        {
            //Walking Sprite
            this.Owner.Sprite.Color = Real4(1,1,1,1);
            this.Collider.Offset = Real3(0, 0, 0);
            this.Collider.Size = this.ColliderSize;
        }
        
        if(this.Owner.PlayerStatus.Pulling)
        {
            this.Owner.Sprite.Color = Real4(0,0,1,1);
        }
        
        //Left Right movement
        
        if(this.Controls.Pad.LeftStick.X != 0)
        {
            movement.X += this.Controls.Pad.LeftStick.X;
        }
        else
        {
            if(this.Controls.KeyDown(0))
            {
                movement.X += -1;
                
                if(this.Owner.PlayerStatus.Pulling) movement.X /= 2;
                
                this.Displacement = Real3(-1.5, 0, 0);
                if(this.Grabbox.PlayerInteraction.CurrentItem != null)
                {
                    this.Grabbox.PlayerInteraction.CurrentItem.FollowObject.Displacement = this.Displacement;
                }
            }
            if(this.Controls.KeyDown(1))
            {
                movement.X += 1;
                
                if(this.Owner.PlayerStatus.Pulling) movement.X /= 2;
                
                this.Displacement = Real3(1.5, 0, 0);
                if(this.Grabbox.PlayerInteraction.CurrentItem != null)
                {
                    this.Grabbox.PlayerInteraction.CurrentItem.FollowObject.Displacement = this.Displacement;
                }
            }
        }
        
        //Console.WriteLine(movement);
        
        if (this.Socks.Grounded()) {
            if (movement.X == 0)
                this.Owner.Sprite.SpriteSource = SpriteSource.Walk1;
            else if (this.Owner.Sprite.SpriteSource != SpriteSource.Walk)
                this.Owner.Sprite.SpriteSource = SpriteSource.Walk;
        } else {
            this.Owner.Sprite.SpriteSource = SpriteSource.Walk1;
        }
        
        if (movement.X > 0) {
            this.Owner.Sprite.FlipX = false;
        } else if (movement.X < 0) {
            this.Owner.Sprite.FlipX = true;
        }
        
        this.Owner.Physocks.Update(movement);
        
        //this.Owner.SweptController.Update(movement, event.Dt);
    }
}

class Input
{
    var Keys : Array[Array[Integer]];
    var IsKey : Array[Array[Boolean]];
    
    var Pad : Gamepad;
    
    constructor()
    {
        this.Pad = Zero.Gamepads.GetGamePad(0);
        this.Keys = new Array[Array[Integer]]();
        this.IsKey = new Array[Array[Boolean]]();
        for (var i=0; i<4; i+=1) {
            this.Keys.Add(new Array[Integer]());
            this.IsKey.Add(new Array[Boolean]());
        }
        // Left keys
        this.AddKey(0, Keys.Left, true);
        this.AddKey(0, Keys.A, true);
        this.AddKey(0, Buttons.DpadLeft, false);
        // Right keys
        this.AddKey(1, Keys.Right, true);
        this.AddKey(1, Keys.D, true);
        this.AddKey(1, Buttons.DpadRight, false);
        // Jump keys
        this.AddKey(2, Keys.Up, true);
        this.AddKey(2, Keys.W, true);
        this.AddKey(2, Buttons.A, false);
        this.AddKey(2, Buttons.DpadUp, false);
        // Crawl keys
        this.AddKey(3, Keys.Down, true);
        this.AddKey(3, Keys.S, true);
        this.AddKey(3, Buttons.DpadDown, false);
        this.AddKey(3, Buttons.RightShoulder, false);
    }
    
    function AddKey(control : Integer, key : Integer, isKey : Boolean)
    {
        this.Keys[control].Add(key);
        this.IsKey[control].Add(isKey);
    }
    
    function KeyDown(key : Integer) : Boolean
    {
        for (var i=0; i<this.IsKey[key].Count; i+=1) {
            if (this.IsKey[key][i]) {
                if (Zero.Keyboard.KeyIsDown(this.Keys[key][i] as Keys))
                    return true;
            } else if (this.Pad.IsActive) {
                if (this.Pad.IsButtonHeld(this.Keys[key][i]))
                    return true;
            }
        }
        return false;
    }
    
    function KeyPressed(key : Integer) : Boolean
    {
        for (var i=0; i<this.IsKey[key].Count; i+=1) {
            if (this.IsKey[key][i]) {
                if (Zero.Keyboard.KeyIsPressed(this.Keys[key][i] as Keys))
                    return true;
            } else if (this.Pad.IsActive) {
                if (this.Pad.IsButtonPressed(this.Keys[key][i]))
                    return true;
            }
        }
        return false;
    }
    
    function KeyReleased(key : Integer) : Boolean
    {
        for (var i=0; i<this.IsKey[key].Count; i+=1) {
            if (this.IsKey[key][i]) {
                if (Zero.Keyboard.KeyIsReleased(this.Keys[key][i] as Keys))
                    return true;
            } else if (this.Pad.IsActive) {
                if (this.Pad.IsButtonReleased(this.Keys[key][i]))
                    return true;
            }
        }
        return false;
    }
    
    
    function SwitchPad(num : Integer) {
        if (Zero.Gamepads.GetGamePad(num).IsActive)
            this.Pad = Zero.Gamepads.GetGamePad(num);
    }
    
    function PadActive() : Boolean {
        return this.Pad.IsActive;
    }
}