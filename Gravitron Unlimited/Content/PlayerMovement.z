// Name: Charles
// Date: 2/2/17
// All content Â© 2017 DigiPen (USA) Corporation, all rights reserved.

class PlayerMovement : ZilchComponent
{
    var MaxMoveSpeed : Real = 0;
    var CeilingDistance : Real = 0;
    var CrawlOffset : Real3 = Real3(0, -0.25, 0);
    
    var Hitbox : Cog = null;    //For holding items
    var Displacement : Real3 = Real3(1.5,0,0);
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        this.MaxMoveSpeed = this.Owner.SweptController.MaxMoveSpeed;
        this.Hitbox = this.Space.FindObjectByName("Hitbox");
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        if(this.Owner.PlayerHealth.Health <= 0)
        {
            return;
        }
        var movement = Real3(0,0,0);
        
        //Jumping
        if(Zero.Keyboard.KeyIsPressed(Keys.W) || Zero.Keyboard.KeyIsPressed(Keys.Up))
        {
            this.Owner.SweptController.Jump();
        }
        if(Zero.Keyboard.KeyIsReleased(Keys.W) || Zero.Keyboard.KeyIsReleased(Keys.Up))
        {
            this.Owner.SweptController.JumpCancel();
        }
        
        //Crawling
        if((Zero.Keyboard.KeyIsDown(Keys.S) || Zero.Keyboard.KeyIsDown(Keys.Down)) && this.Owner.SweptController.Grounded)
        {
            this.Owner.SweptController.MaxMoveSpeed = 5;        //Crawling Speed
            //Crawling Sprite
            
                this.Owner.Sprite.Color = Real4(1,0,0,1);
                this.Owner.BoxCollider.Size = Real3(2, 0.5, 1);
                this.Owner.BoxCollider.Offset = this.CrawlOffset;
        }
        if(Zero.Keyboard.KeyIsReleased(Keys.S) || Zero.Keyboard.KeyIsReleased(Keys.Down))
        {
            this.Owner.SweptController.MaxMoveSpeed = this.MaxMoveSpeed;
            //Walking Sprite
            
                this.Owner.Sprite.Color = Real4(1,1,1,1);
                this.Owner.BoxCollider.Size = Real3(1, 1, 1);
                this.Owner.BoxCollider.Offset = Real3(0, 0, 0);
        }
        
        //Left Right movemennt
        if(Zero.Keyboard.KeyIsDown(Keys.A) || Zero.Keyboard.KeyIsDown(Keys.Left))
        {
            movement = Real3(-1,0,0);
            this.Displacement = Real3(-1.5, 0, 0);
            if(this.Hitbox.PlayerInteraction.CurrentItem != null)
            {
                this.Hitbox.PlayerInteraction.CurrentItem.FollowObject.Displacement = this.Displacement;
            }
        }
        else if(Zero.Keyboard.KeyIsDown(Keys.D) || Zero.Keyboard.KeyIsDown(Keys.Right))
        {
            movement = Real3(1,0,0);
            this.Displacement = Real3(1.5, 0, 0);
            if(this.Hitbox.PlayerInteraction.CurrentItem != null)
            {
                this.Hitbox.PlayerInteraction.CurrentItem.FollowObject.Displacement = this.Displacement;
            }
        }
        
        this.Owner.SweptController.Update(movement, event.Dt);
    }
}
