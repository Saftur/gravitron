class Physocks : ZilchComponent
{
    [Dependency]
    var Trans : Transform = null;
    [Dependency]
    var Collider : BoxCollider = null;
    [Dependency]
    var RBody : RigidBody = null;
    
    [Property]
    var Speed : Real = 5;
    
    [Property]
    var JumpStrength : Real = 15;
    function Initialize(init : CogInitializer)
    {
    }

    function Update(movement : Real3)
    {
        var yvel = this.RBody.Velocity.Y;
        this.RBody.Velocity = movement*Real3(this.Speed,1,1)+Real3(0, yvel, 0);
        //Console.WriteLine("`this.Trans.Translation`");
        //Console.WriteLine(this.Grounded());
    }
    
    function Jump()
    {
        var tmp = this.RBody.Velocity;
        tmp.Y = this.JumpStrength * -this.Space.LevelSettings.GravityEffect.Direction.Y;
        this.RBody.Velocity = tmp;
    }
    
    function Grounded() : Boolean
    {
        var pos = this.Trans.Translation+this.Collider.Offset;
        var width = this.Collider.Size.X;
        var dist = this.Collider.Size.Y/2;
        var ray = Ray();
        ray.Direction = this.Space.LevelSettings.GravityEffect.Direction;
        ray.Start = pos;
        if (this.CastGroundRay(ray, dist)) return true;
        ray.Start = pos + Real3(width/2-0.05, 0, 0); // -0.1 is wall jump fix, remove when fixing going in walls
        if (this.CastGroundRay(ray, dist)) return true;
        ray.Start = pos + Real3(-width/2+0.05, 0, 0);
        if (this.CastGroundRay(ray, dist)) return true;
        return false;
    }
    
    function UnderBlock() : Boolean
    {
        var pos = this.Trans.Translation+this.Collider.Offset;
        var width = this.Collider.Size.X;
        var dist = this.Collider.Size.Y/2+0.05;
        var ray = Ray();
        ray.Direction = this.Space.LevelSettings.GravityEffect.Direction * Real3(1, -1, 1);
        ray.Start = pos;
        if (this.CastGroundRay(ray, dist)) return true;
        ray.Start = pos + Real3(width/2-0.05, 0, 0); // -0.1 is wall jump fix, remove when fixing going in walls
        if (this.CastGroundRay(ray, dist)) return true;
        ray.Start = pos + Real3(-width/2+0.05, 0, 0);
        if (this.CastGroundRay(ray, dist)) return true;
        return false;
    }
    
    function InPit() : Boolean
    {
        var pos = this.Trans.Translation+this.Owner.PlayerMovement.CrawlOffset;
        var height = this.Owner.PlayerMovement.ColliderCrawlSize.Y;
        var dist = this.Owner.PlayerMovement.ColliderCrawlSize.X/2;
        var hitLeft = false;
        var hitRight = false;
        var ray = Ray();
        ray.Direction = Real3(-1, 0, 0);
        ray.Start = pos;
        if (this.CastGroundRay(ray, dist)) hitLeft = true;
        ray.Start = pos + Real3(0, height/2-0.05, 0); // -0.1 is wall jump fix, remove when fixing going in walls
        if (this.CastGroundRay(ray, dist)) hitLeft = true;
        ray.Start = pos + Real3(0, -height/2+0.05, 0);
        if (this.CastGroundRay(ray, dist)) hitLeft = true;
        ray.Direction = Real3(1, 0, 0);
        ray.Start = pos;
        if (this.CastGroundRay(ray, dist)) hitRight = true;
        ray.Start = pos + Real3(0, height/2-0.05, 0); // -0.1 is wall jump fix, remove when fixing going in walls
        if (this.CastGroundRay(ray, dist)) hitRight = true;
        ray.Start = pos + Real3(0, -height/2+0.05, 0);
        if (this.CastGroundRay(ray, dist)) hitRight = true;
        if (hitLeft && hitRight) return true;
        return false;
    }
    
    function CastGroundRay(ray : Ray, dist : Real) : Boolean
    {
        var rayDist = this.CastGroundRay(ray);
        if (rayDist == -1) return false;
        else return (rayDist <= dist);
    }
    
    function CastGroundRay(ray : Ray) : Real
    {
        var objHit = this.Space.PhysicsSpace.CastRayFirst(ray);
        if(objHit != null)
        {
            //Console.WriteLine("`this.Owner.Name`: `objHit.Distance`");
            return objHit.Distance;
        }
        return -1;
    }
}
